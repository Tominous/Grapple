#	  sSSSSs   .S_sSSs     .S_SSSs     .S_sSSs     .S_sSSs    S.        sSSs
#	 d%%%%SP  .SS~YS%%b   .SS~SSSSS   .SS~YS%%b   .SS~YS%%b   SS.      d%%SP
#	d%S'      S%S   `S%b  S%S   SSSS  S%S   `S%b  S%S   `S%b  S%S     d%S'  
#	S%S       S%S    S%S  S%S    S%S  S%S    S%S  S%S    S%S  S%S     S%S   
#	S&S       S%S    d*S  S%S SSSS%S  S%S    d*S  S%S    d*S  S&S     S&S   
#	S&S       S&S   .S*S  S&S  SSS%S  S&S   .S*S  S&S   .S*S  S&S     S&S_Ss
#	S&S       S&S_sdSSS   S&S    S&S  S&S_sdSSS   S&S_sdSSS   S&S     S&S~SP
#	S&S sSSs  S&S~YSY%b   S&S    S&S  S&S~YSSY    S&S~YSSY    S&S     S&S   
#	S*b `S%%  S*S   `S%b  S*S    S&S  S*S         S*S         S*b     S*b   
#	S*S   S%  S*S    S%S  S*S    S*S  S*S         S*S         S*S.    S*S.  
#	 SS_sSSS  S*S    S&S  S*S    S*S  S*S         S*S          SSSbs   SSSbs
#	  Y~YSSY  S*S    SSS  SSS    S*S  S*S         S*S           YSSP    YSSP
#	          SP                 SP   SP          SP                        
#	          Y                  Y    Y           Y                         
#
#		 _                                      
#		| |                                     
#		| |__  _   _   _ __ ___   ___  _____  __
#		| '_ \| | | | | '_ ` _ \ / _ \/ _ \ \/ /
#		| |_) | |_| | | | | | | |  __/  __/>  < 
#		|_.__/ \__, | |_| |_| |_|\___|\___/_/\_\
#		        __/ |                           
#		       |___/                            

# OPTIONS
options:
	prefix: "&c[&6Grapple&c]&r" # The prefix for the commands
	message: "&fYou gave &e[player]&f a &6grappling hook&f." # What the give message would be.

	name: "&6Grappling Hook" # What the name of the grappling hook should be
	glowing: true

	self velocity: 2 # How fast and far the player will go when the player grapple's
	players velocity: 2 # How fast and far the player's will go when a player grapple's them
	entity velocity: 3 # How fast and far entity's will go when a player grapple's them
	item velocity: 1 # How fast and far an item will go when a player grapple's it

	only in ground: false # If the grappling hook needs to be in the ground to work * It's glitchy if it's enabled










#
#
# Edit this if you know what you're doing.
#
#



command /grapplinghook <player=%player%>:
	aliases: /gh, /grapple
	description: Receive a grapplinghook.
	permission:	grapple.give
	executable by: players
	trigger:
		set {_message} to join {@prefix} and {@message} with delimiter " "
		replace all "[player]" with "%arg-1%" in {_message}
		message {_message} to player
		if {@glowing} is true:
			give arg-1 1 of glowing fishing rod named {@name}
		else:
			give arg-1 1 of fishing rod named {@name}


on player fishing:
	if name of tool of player is {@name}:
		if caught entity is not set:
			if {@only in ground} is true:
				if fishing state is in ground:
					if player has the permission "grapple.pull.self":
						push player (direction from player to location of hook) at speed {@self velocity}
			else:
				if fishing state is not fishing:
					if player has the permission "grapple.pull.self":
						push player (direction from player to location of hook) at speed {@self velocity}
		if caught entity is set:
			if caught entity is a dropped item:
				if player has the permission "grapple.pull.items":
					push caught entity (direction from caught entity to location of player) at speed {@item velocity}
			else if caught entity is a player:
				if caught entity doesn't have permission "grapple.nopull":
					if player has the permission "grapple.pull.players":
						push caught entity (direction from caught entity to location of player) at speed {@players velocity}
			else:
				if player has the permission "grapple.pull.entities":
					push caught entity (direction from caught entity to location of player) at speed {@entity velocity}